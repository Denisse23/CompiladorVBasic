package pc;
import java_cup.runtime.*;
import java_cup.runtime.Symbol;
import pc.treeelements.*;
import java.util.ArrayList;
import pc.Tipos.*;

parser code {:
    TablasDeSimbolos tds;
    public void setTablasDeSimbolos(TablasDeSimbolos t){
        tds=t;
    }

    public  int errors_count =0;
    public void error_tipos_diferentes_exp(String exp1,String exp2,  String op, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", no se pudo realizar la operación: "+op+", la expresión: "+exp1+" y la expresión: "+exp2+" son tipos diferentes): Semantic error");
    }

    public void error_tipo_no_numerico_exp(String exp1,String exp2, String op, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", no se pudo realizar la operación: "+op+", la expresión: "+exp1+" y la expresión: "+exp2+" no son de tipo númerico): Semantic error");
    }
    public void error_argumentos_incorrectos (String id,String argsenviados,String argsesperados, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", se encontraron los argumentos de tipo: "+argsenviados+" en la llamada al procedimiento o función: "+id+", donde se esperaba: "+argsesperados+"): Semantic error");
    }

    public void error_write_void  (String exp,String tipo, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", no se pudo imprimir la expresión: "+exp+" que es de tipo: "+tipo+"): Semantic error");
    }

    public void error_asignacion (String id, String tipoid, String exp, String tipoexp, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", no se pudo asignar al identificador: "+id+" de tipo: "+tipoid+", la expresión: "+exp+" de tipo: "+tipoexp+"): Semantic error");
    }

    public void  error_asignacion_for (int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", la asignación en el for tiene que ser de tipo númerico): Semantic error");
    }

    public void error_tipo_no_numerico(String exp, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", expresión: "+exp+" no es de tipo númerico): Semantic error");
    }

    public void error_tipo_no_booleano (String exp, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", expresión: "+exp+" no es de tipo booleano): Semantic error");
    }
    public void error_no_main(){
        errors_count++;
        System.err.println("Error (No se encontro el procedimiento main): Semantic error");
    }
    public void error_valor_return (String id, String tipoid, String exp, String tipoexp, int line, int column){
        errors_count++;
        System.err.println("Error (line: "+line+", column: "+column+", el tipo de retorno de la función: "+id+" es: "+tipoid+", y la expresión: "+exp+" es de tipo: "+tipoexp+"): Semantic error");
    }

    
    public Programa programaT;
    public String ambito_actual="1";

:};


terminal sub, abrirparentesis, cerrarparentesis, write, read, console, punto, type,  asignacion;
terminal function,  coma, end,  then, to, next,  loop, dim, as, not;
terminal Do, If, For, While, Return, Else; //palabras reservadas de java

terminal String tipoparametro, tipovar, truefalse, oplog, oprel, opsum, opmult, identificador;
terminal String espacio, newline, tab, cadena, comentario, numero;


non terminal Programa INICIO;
non terminal Programa PROGRAMA;
non terminal Programa PROGRAMA2;
non terminal INDENTACION;
non terminal INDENTACION2;
non terminal ArrayList<Node> DECLARACION_VARIABLE;
non terminal ArrayList<Node> DECLARACION_VARIABLE2;
non terminal Stmt_Funcion BLOQUE_FUNCION;
non terminal Stmt_Llamada_Funcion BLOQUE_LLAMADAS_FUNCIONES;
non terminal Stmt_Procedimiento BLOQUE_PROCEDIMIENTO;
non terminal ArrayList<Node> LISTA_PARAMENTROS;
non terminal ArrayList<Node> CONTENIDO_SUB;
non terminal ArrayList<Node> CONTENIDO_FUNCTION;
non terminal Stmt_Return BLOQUE_RETURN;
non terminal Stmt_If BLOQUE_IF;
non terminal Stmt_If BLOQUE_IF_FUNCTION;
non terminal Node TIPOS;
non terminal Exp VALORES;
non terminal ArrayList<Node> ARGUMENTOS;
non terminal Exp EXP;
non terminal Condition EXP_CONDICIONALES;
non terminal ArrayList<Node> CONTENIDO_IF;
non terminal ArrayList<Node> CONTENIDO_IF_FUNCTION;
non terminal Stmt_Asignacion BLOQUE_ASIGNACION;
non terminal Stmt_For BLOQUE_FOR;
non terminal Stmt_For BLOQUE_FOR_FUNCTION;
non terminal Stmt_Asignacion_For BLOQUE_ASIGNACION_FOR;
non terminal ArrayList<Node> CONTENIDO_FOR;
non terminal ArrayList<Node> CONTENIDO_FOR_FUNCTION;
non terminal Stmt_While BLOQUE_WHILE;
non terminal Stmt_While BLOQUE_WHILE_FUNCTION;
non terminal ArrayList<Node> CONTENIDO_WHILE;
non terminal ArrayList<Node> CONTENIDO_WHILE_FUNCTION;
non terminal Stmt_Type BLOQUE_ESTRUCTURA;
non terminal ArrayList<Node> CONTENIDO_ESTRUCTURA;
non terminal Stmt_Variable DECLARACION_VARIABLE_ESTRUCTURA;
non terminal Stmt_Lectura BLOQUE_LECTURA;
non terminal Stmt_Escritura BLOQUE_ESCRITURA;
non terminal Stmt_Comentario BLOQUE_COMENTARIOS;
non terminal String ELEMENTOS_ESTRUCTUA;



precedence left opsum;
precedence left espacio;
precedence left opmult;

precedence left not, oplog;

INICIO::=PROGRAMA:p {:programaT = p; if(!tds.hay_main()){ error_no_main();} :}
         |newline PROGRAMA:p {:RESULT = p; programaT = p; if(!tds.hay_main()){ error_no_main();}:}
         ;

PROGRAMA::=INDENTACION PROGRAMA:p {:RESULT = p;:}
           |INDENTACION PROGRAMA2:p2 {:RESULT = p2;:}
           |BLOQUE_PROCEDIMIENTO:bp INDENTACION2 PROGRAMA2:p2 {:RESULT=p2; RESULT.addNode(bp); :}
           |BLOQUE_FUNCION:bf INDENTACION2 PROGRAMA2:p2 {:RESULT = p2; RESULT.addNode(bf);:}
           |BLOQUE_ESTRUCTURA:be INDENTACION2 PROGRAMA2:p2 {:RESULT = p2; RESULT.addNode(be);:}
           |DECLARACION_VARIABLE:dv INDENTACION2 PROGRAMA2:p2 {:RESULT = p2; RESULT.addListNode(dv); :}
           |BLOQUE_COMENTARIOS:bc INDENTACION2 PROGRAMA2:p2  {:RESULT = p2; RESULT.addNode(bc);:}
           ;
PROGRAMA2::=newline PROGRAMA:p {:RESULT = p;:}
            |newline {:RESULT = new Programa("Programa");:}
            | {:RESULT = new Programa("Programa");:}
            ;

CONTENIDO_SUB::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_SUB:cs  {:RESULT = cs; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_IF:bi INDENTACION2  CONTENIDO_SUB:cs {:RESULT = cs; RESULT.add(bi); :}
               |newline INDENTACION2 BLOQUE_FOR:bf INDENTACION2  CONTENIDO_SUB:cs {:RESULT = cs; RESULT.add(bf); :}
               |newline INDENTACION2 DECLARACION_VARIABLE:dv INDENTACION2  CONTENIDO_SUB:cs  {:RESULT = cs; RESULT.addAll(dv); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_SUB:cs {:RESULT = cs; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_WHILE:bw INDENTACION2 CONTENIDO_SUB:cs {:RESULT = cs; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_SUB:cs {:RESULT = cs; :}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_SUB:cs {:RESULT =cs; RESULT.add(ba);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_SUB:cs {:RESULT = cs; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_SUB:cs {:RESULT =cs; RESULT.add(bllf);:}
               ;


CONTENIDO_FUNCTION::=newline INDENTACION2 DECLARACION_VARIABLE:dv INDENTACION2  CONTENIDO_FUNCTION:cf  {:RESULT = cf; RESULT.addAll(dv); :}
               |newline INDENTACION2 BLOQUE_FOR_FUNCTION:bf INDENTACION2  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(bf); :}
               |newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_IF_FUNCTION:bi INDENTACION2  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(bi); :}
               |newline INDENTACION2 BLOQUE_WHILE_FUNCTION:bw INDENTACION2  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_FUNCTION:cf {:RESULT = cf; :}
               |newline INDENTACION2 BLOQUE_RETURN:br  CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(br);:}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(ba);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_FUNCTION:cf {:RESULT = cf; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_FUNCTION:cf {:RESULT =cf; RESULT.add(bllf);:}
               ;



BLOQUE_RETURN::= Return espacio EXP:e {:RESULT = new Stmt_Return("return"); RESULT.addNode(e);
                                            String id_funcion = tds.get_id_segun_ambito_hijos(ambito_actual);
                                            if(!((Funcion)tds.getTabla("Principal").get_id(id_funcion).getTipo()).getRetorno().toString().equals(e.getTipo_tabla().toString())){
                                                error_valor_return(id_funcion, ((Funcion)tds.getTabla("Principal").get_id(id_funcion).getTipo()).getRetorno().toString(), e.getVal(), e.getTipo_tabla().toString(), eleft, eright );
                                            }
                                      :}
         ; 

INDENTACION2::=INDENTACION INDENTACION2
               |
               ;

INDENTACION::=tab
              |espacio
              ;

DECLARACION_VARIABLE::=dim espacio DECLARACION_VARIABLE2:dv2 {:RESULT = dv2;:}
                       ;

DECLARACION_VARIABLE2::=identificador:id espacio as espacio TIPOS:t coma espacio  DECLARACION_VARIABLE2:dv2  {:RESULT = dv2; RESULT.add(new Stmt_Variable("var")); RESULT.get(RESULT.size()-1).addNode(new Node(id)); RESULT.get(RESULT.size()-1).addNode(t); :}
                               |identificador:id coma espacio DECLARACION_VARIABLE2:dv2 {:RESULT = dv2; RESULT.add(new Stmt_Variable("var")); RESULT.get(RESULT.size()-1).addNode(new Node(id)); Stmt_Variable temp = (Stmt_Variable)RESULT.get(RESULT.size()-2); RESULT.get(RESULT.size()-1).addNode(temp.getTipo()); :}
                               |identificador:id espacio as:as espacio TIPOS:t  {:RESULT = new ArrayList(); RESULT.add(new Stmt_Variable("var")); RESULT.get(RESULT.size()-1).addNode(new Node(id)); RESULT.get(RESULT.size()-1).addNode(t);:}

                               ;

TIPOS::=tipovar:tv {:RESULT = new Node(tv);:}
          |identificador:id  {:RESULT = new Node(id);:}
          ;

BLOQUE_PROCEDIMIENTO::=sub:s espacio identificador:id abrirparentesis cerrarparentesis INDENTACION2 {:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_SUB:cs end espacio sub {:RESULT = new Stmt_Procedimiento("sub"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters")); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cs); :}
                       |sub espacio identificador:id abrirparentesis LISTA_PARAMENTROS:lp cerrarparentesis INDENTACION2 {:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_SUB:cs end espacio sub  {:RESULT = new Stmt_Procedimiento("sub"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters")); RESULT.getParameters().setNodes(lp); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cs);:}
                ;
 
LISTA_PARAMENTROS::=tipoparametro:tp espacio identificador:id espacio as espacio tipovar:t {:RESULT = new ArrayList(); RESULT.add(new Stmt_Parametro("parameter")); RESULT.get(RESULT.size()-1).addNode(new Node(tp)); RESULT.get(RESULT.size()-1).addNode(new Node(id)); RESULT.get(RESULT.size()-1).addNode(new Node(t));:}
                    |tipoparametro:tp espacio identificador:id espacio as espacio tipovar:t coma espacio LISTA_PARAMENTROS:lp {:RESULT = lp; RESULT.add(new Stmt_Parametro("parameter")); RESULT.get(RESULT.size()-1).addNode(new Node(tp)); RESULT.get(RESULT.size()-1).addNode(new Node(id)); RESULT.get(RESULT.size()-1).addNode(new Node(t));:}
                    ;

BLOQUE_FUNCION::=function espacio identificador:id abrirparentesis LISTA_PARAMENTROS:lp cerrarparentesis espacio as espacio TIPOS:t INDENTACION2 {:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_FUNCTION:cf end espacio function  {:RESULT = new Stmt_Funcion("function"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters")); RESULT.getParameters().setNodes(lp); RESULT.addNode(t); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cf);:}
                 |function espacio identificador:id abrirparentesis cerrarparentesis espacio as espacio TIPOS:t INDENTACION2 {:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_FUNCTION:cf end espacio function {:RESULT = new Stmt_Funcion("function"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters")); RESULT.addNode(t); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cf);:}
                 |function espacio identificador:id abrirparentesis cerrarparentesis INDENTACION2{:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_SUB:cs end espacio function {:RESULT = new Stmt_Funcion("function"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters"));  RESULT.addNode(new Node("void")); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cs);:}
                 |function espacio identificador:id abrirparentesis LISTA_PARAMENTROS:lp cerrarparentesis INDENTACION2 {:ambito_actual=tds.get_ambito_hijos_id(id);:} CONTENIDO_SUB:cs end espacio function  {:RESULT = new Stmt_Funcion("function"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("parameters")); RESULT.getParameters().setNodes(lp);  RESULT.addNode(new Node("void")); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(cs);:}
          ;

BLOQUE_LLAMADAS_FUNCIONES::=identificador:id abrirparentesis cerrarparentesis {:RESULT = new Stmt_Llamada_Funcion("call"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("arguments"));
                                                                               if(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).toStringSinRetorno().equals("Void")){
                                                                                    RESULT.setTipo_tabla(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).getRetorno());
                                                                               }else{
                                                                                     RESULT.setTipo_tabla(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).getRetorno());
                                                                                     error_argumentos_incorrectos(id, "Void",((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).toStringSinRetorno() , idleft, idright);
                                                                               } :}
                            |identificador:id abrirparentesis ARGUMENTOS:a cerrarparentesis {:RESULT = new Stmt_Llamada_Funcion("call"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("arguments")); RESULT.getArguments().addListNode(a);
                                                                                                    Tipo t = new Producto();
                                                                                                    for(int i=RESULT.getArguments().getListNode().size()-1; i>=0; i--){
                                                                                                        ((Producto)t).addArgumento(RESULT.getArguments().getListNode().get(i).getTipo_tabla());
                                                                                                    }
                                                                                                    if(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).toStringSinRetorno().equals(t.toString())){
                                                                                                        RESULT.setTipo_tabla(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).getRetorno());
                                                                                                    }else{
                                                                                                        RESULT.setTipo_tabla(((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).getRetorno());
                                                                                                        error_argumentos_incorrectos(id,t.toString(),((Funcion)tds.get_id_ambitos(id, "1", "Principal").getTipo()).toStringSinRetorno() , idleft, idright);
                                                                                                    }
                                                                                                    
                                                                                            :}
                  ;

ARGUMENTOS::=EXP:e {:RESULT = new ArrayList(); RESULT.add(new Stmt_Argumento("argument")); RESULT.get(RESULT.size()-1).addNode(e);  RESULT.get(RESULT.size()-1).setTipo_tabla(e.getTipo_tabla());:}
             |EXP:e coma espacio ARGUMENTOS:a {:RESULT =a; RESULT.add(new Stmt_Argumento("argument")); RESULT.get(RESULT.size()-1).addNode(e); RESULT.get(RESULT.size()-1).setTipo_tabla(e.getTipo_tabla());:}
             ;


EXP_CONDICIONALES::=EXP:e oprel:opr espacio EXP:e2 {:RESULT = new Condition(opr); RESULT.addNode(e); RESULT.addNode(e2);
                    if(e.getTipo_tabla().toString().equals(e2.getTipo_tabla().toString())){
                        if(e.getTipo_tabla().toString().equals("Integer")){
                            RESULT.setTipo_tabla(new booleant());}
                        else{
                            ///ya tiene tipo void por default 
                            error_tipo_no_numerico_exp(e.getVal(), e2.getVal(),opr, eleft, eright);
                        }

                     }else{
                        ///ya tiene tipo void por default 
                        error_tipos_diferentes_exp(e.getVal(), e2.getVal(), opr,eleft, eright);
                      }
                    :}
                    |EXP:e asignacion:a espacio EXP:e2 {:RESULT = new Condition("="); RESULT.addNode(e); RESULT.addNode(e2);
                     if(e.getTipo_tabla().toString().equals(e2.getTipo_tabla().toString())){
                        RESULT.setTipo_tabla(new booleant());
                     }else{
                        ///ya tiene tipo void por default 
                        error_tipos_diferentes_exp(e.getVal(), e2.getVal(), "=",eleft, eright);
                      }

                     :} 
                    |EXP_CONDICIONALES:ec oplog:ol espacio EXP_CONDICIONALES:ec2 {:RESULT = new Condition(ol); RESULT.addNode(ec); RESULT.addNode(ec2);
                        if(ec.getTipo_tabla().toString().equals(ec2.getTipo_tabla().toString())){
                        if(ec.getTipo_tabla().toString().equals("Boolean")){
                            RESULT.setTipo_tabla(new booleant());}
                        else{
                            ///ya tiene tipo void por default 
                            error_tipo_no_numerico_exp(ec.getVal(), ec2.getVal(),ol, ecleft, ecright);
                        }

                     }else{
                        ///ya tiene tipo void por default 
                        error_tipos_diferentes_exp(ec.getVal(), ec2.getVal(), ol,ecleft, ecright);
                      }
                    :}
                    ;   


EXP::= EXP:e opsum:os espacio EXP:e2 {:RESULT = new Exp(os); RESULT.addNode(e); RESULT.addNode(e2); 

                                    if(e.getTipo_tabla().toString().equals(e2.getTipo_tabla().toString())){
                                        if(e.getTipo_tabla().toString().equals("Integer")){
                                            RESULT.setTipo_tabla(e.getTipo_tabla());}
                                        else{
                                            RESULT.setTipo_tabla(e.getTipo_tabla());
                                            error_tipo_no_numerico_exp(e.getVal(), e2.getVal(),os, eleft, eright);
                                        }
                                      }else{
                                        RESULT.setTipo_tabla(e.getTipo_tabla());
                                        error_tipos_diferentes_exp(e.getVal(), e2.getVal(),os, eleft, eright);
                                      }:}
       |EXP:e opmult:om espacio EXP:e2 {:RESULT = new Exp(om); RESULT.addNode(e); RESULT.addNode(e2);
                                       if(e.getTipo_tabla().toString().equals(e2.getTipo_tabla().toString())){
                                        if(e.getTipo_tabla().toString().equals("Integer")){
                                            RESULT.setTipo_tabla(e.getTipo_tabla());}
                                        else{
                                            RESULT.setTipo_tabla(e.getTipo_tabla());
                                            error_tipo_no_numerico_exp(e.getVal(), e2.getVal(),om, eleft, eright);
                                        }
                                      }else{
                                        RESULT.setTipo_tabla(e.getTipo_tabla());
                                        error_tipos_diferentes_exp(e.getVal(), e2.getVal(),om, eleft, eright);
                                      }:}
       |VALORES:v INDENTACION2 {:RESULT =  v; :}
       |not espacio VALORES:v INDENTACION2 {:RESULT = new Not("not"); RESULT.addNode(v); RESULT.setTipo_tabla(v.getTipo_tabla());:}
       ;

VALORES::=identificador:id {:RESULT = new Exp(id);RESULT.setTipo_tabla(tds.get_id_ambitos(id, ambito_actual, "Principal").getTipo());:}
          |truefalse:tf {:RESULT = new Exp(tf); RESULT.setTipo_tabla(new booleant()); :}
          |numero:n {:RESULT = new Exp(n); RESULT.setTipo_tabla(new integert()); :}
          |cadena:c {:RESULT = new Exp(c);  RESULT.setTipo_tabla(new stringt()); :}
          |ELEMENTOS_ESTRUCTUA:ee {:RESULT = new Exp(ee); RESULT.setTipo_tabla(tds.get_id_estructura(ee, ambito_actual).getTipo());:}
          |abrirparentesis EXP:e cerrarparentesis {:RESULT = e; RESULT.setTipo_tabla(e.getTipo_tabla());:}
          |BLOQUE_LLAMADAS_FUNCIONES:bllf {: RESULT = bllf; RESULT.setTipo_tabla(bllf.getTipo_tabla()); :}
          ;

ELEMENTOS_ESTRUCTUA::= ELEMENTOS_ESTRUCTUA:ee punto identificador:id {: RESULT=ee+"."+id; :}
                       |identificador:id punto identificador:id2 {:RESULT=""; RESULT+=id+"."+id2;:}
                       ;

CONTENIDO_IF::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_FOR:bf INDENTACION2  CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(bf); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_WHILE:bw INDENTACION2 CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_IF:ci {:RESULT = ci;:}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(ba);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_IF:ci {:RESULT = ci; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_IF:ci {:RESULT =ci; RESULT.add(bllf);:}
               ;
CONTENIDO_IF_FUNCTION::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_FOR_FUNCTION:bf INDENTACION2  CONTENIDO_IF_FUNCTION:ci  {:RESULT = ci; RESULT.add(bf); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_WHILE_FUNCTION:bw INDENTACION2 CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_IF_FUNCTION:ci {:RESULT = ci;:}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(ba);:}
               |newline INDENTACION2 BLOQUE_RETURN:br CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(br);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_IF_FUNCTION:ci {:RESULT = ci; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_IF_FUNCTION:ci {:RESULT =ci; RESULT.add(bllf);:}
               ;


BLOQUE_IF::=If espacio EXP_CONDICIONALES:ec  then  CONTENIDO_IF:ci end {:if(!ec.getTipo_tabla().toString().equals("Boolean")){error_tipo_no_booleano(ec.getVal(), ecleft, ecright);}:} espacio If {:RESULT = new Stmt_If("If"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(ci); RESULT.addNode(new Node("else-body"));:}
            |If espacio EXP_CONDICIONALES:ec then  CONTENIDO_IF:ci Else {:if(!ec.getTipo_tabla().toString().equals("Boolean")){error_tipo_no_booleano(ec.getVal(), ecleft, ecright);}:} INDENTACION2 CONTENIDO_IF:cie end espacio If {:RESULT = new Stmt_If("If"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(ci); RESULT.addNode(new Node("else-body")); RESULT.getElseBody().addListNode(cie);:}
            ;

BLOQUE_IF_FUNCTION::=If espacio EXP_CONDICIONALES:ec then  INDENTACION2 CONTENIDO_IF_FUNCTION:ci end espacio If  {:RESULT = new Stmt_If("If"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(ci); RESULT.addNode(new Node("else-body"));:}
            |If espacio EXP_CONDICIONALES:ec then INDENTACION2 CONTENIDO_IF_FUNCTION:ci Else INDENTACION2 CONTENIDO_IF_FUNCTION:cie end espacio If {:RESULT = new Stmt_If("If"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(ci); RESULT.addNode(new Node("else-body")); RESULT.getElseBody().addListNode(cie);:}
            ;




BLOQUE_ASIGNACION::=identificador:id espacio asignacion espacio EXP:e {:RESULT = new Stmt_Asignacion("="); RESULT.addNode(new Node(id)); RESULT.addNode(e);
                                                                       if(!(tds.get_id_ambitos(id, ambito_actual, "Principal").getTipo().toString().equals(e.getTipo_tabla().toString()))){
                                                                            error_asignacion(id,tds.get_id_ambitos(id, ambito_actual, "Principal").getTipo().toString(), e.getVal(), e.getTipo_tabla().toString(), idleft, idright);
                                                                       }
                                                                       :}
               |ELEMENTOS_ESTRUCTUA:ee espacio asignacion espacio EXP:e  {:RESULT = new Stmt_Asignacion("="); RESULT.addNode(new Node(ee)); RESULT.addNode(e);
                                                                                if(!(tds.get_id_estructura(ee, ambito_actual).getTipo().toString().equals(e.getTipo_tabla().toString()))){
                                                                            error_asignacion(ee,tds.get_id_estructura(ee, ambito_actual).getTipo().toString(), e.getVal(), e.getTipo_tabla().toString(), eeleft, eeright);
                                                                       }
                                                                        :}
               ;

BLOQUE_ASIGNACION_FOR::=identificador:id espacio asignacion espacio EXP:e  {:RESULT = new Stmt_Asignacion_For("="); RESULT.addNode(new Node(id)); RESULT.addNode(e);
                                                                                    if((tds.get_id_ambitos(id, ambito_actual, "Principal").getTipo().toString().equals(e.getTipo_tabla().toString()))){
                                                                                        if(!(e.getTipo_tabla().toString().equals("Integer"))){
                                                                                            error_asignacion_for( idleft, idright);
                                                                                        }
                                                                                    }else{
                                                                                        error_asignacion(id,tds.get_id_ambitos(id, ambito_actual, "Principal").getTipo().toString(), e.getVal(), e.getTipo_tabla().toString(), idleft, idright);
                                                                                    }
                                                                            :}
                        |ELEMENTOS_ESTRUCTUA:ee espacio asignacion espacio EXP:e {:RESULT = new Stmt_Asignacion_For("="); RESULT.addNode(new Node(ee)); RESULT.addNode(e);

                                                                                    if((tds.get_id_estructura(ee, ambito_actual).getTipo().toString().equals(e.getTipo_tabla().toString()))){
                                                                                    if(!(e.getTipo_tabla().toString().equals("Integer"))){
                                                                                            error_asignacion_for( eeleft, eeright);
                                                                                        }
                                                                       }else{
                                                                            error_asignacion(ee,tds.get_id_estructura(ee, ambito_actual).getTipo().toString(), e.getVal(), e.getTipo_tabla().toString(), eeleft, eeright);
                                                                       }
                                                                                  :}
                        ;


BLOQUE_FOR::=For espacio BLOQUE_ASIGNACION_FOR:ba to espacio EXP:e {:if(!e.getTipo_tabla().toString().equals("Integer")){error_tipo_no_numerico(e.getVal(), eleft, eright);}:} CONTENIDO_FOR:cf next {:RESULT = new Stmt_For("for"); RESULT.addNode(ba);  RESULT.addNode(e);RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(cf);:}
             ;


BLOQUE_FOR_FUNCTION::=For espacio BLOQUE_ASIGNACION_FOR:ba to espacio EXP:e  {:if(!e.getTipo_tabla().toString().equals("Integer")){error_tipo_no_numerico(e.getVal(), eleft, eright);}:} CONTENIDO_FOR_FUNCTION:cf next {:RESULT = new Stmt_For("for"); RESULT.addNode(ba);  RESULT.addNode(e);RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(cf);:}
             ;

CONTENIDO_FOR::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_FOR:cf {:RESULT = cf; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_IF:bi INDENTACION2  CONTENIDO_FOR:cf {:RESULT = cf; RESULT.add(bi);:}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_FOR:cf  {:RESULT = cf; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_WHILE:bw INDENTACION2 CONTENIDO_FOR:cf {:RESULT = cf; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_FOR:cf {:RESULT = cf;:}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_FOR:cf {:RESULT = cf; RESULT.add(ba);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_FOR:cf {:RESULT = cf; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_FOR:cf {:RESULT =cf; RESULT.add(bllf);:}
               ;
CONTENIDO_FOR_FUNCTION::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_IF_FUNCTION:bi INDENTACION2  CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(bi);:}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_WHILE_FUNCTION:bw INDENTACION2 CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(bw); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf;:}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(ba);:}
               |newline INDENTACION2 BLOQUE_RETURN:br CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(br);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_FOR_FUNCTION:cf {:RESULT = cf; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_FOR_FUNCTION:cf {:RESULT =cf; RESULT.add(bllf);:}
               ;

BLOQUE_WHILE::=Do espacio While espacio EXP_CONDICIONALES:ec {:if(!ec.getTipo_tabla().toString().equals("Boolean")){error_tipo_no_booleano(ec.getVal(), ecleft, ecright);}:} CONTENIDO_WHILE:cw loop {:RESULT= new Stmt_While("while"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(cw); :}
             ;


BLOQUE_WHILE_FUNCTION::=Do espacio While espacio EXP_CONDICIONALES:ec {:if(!ec.getTipo_tabla().toString().equals("Boolean")){error_tipo_no_booleano(ec.getVal(), ecleft, ecright);}:} CONTENIDO_WHILE_FUNCTION:cw loop {:RESULT= new Stmt_While("while"); RESULT.addNode(ec); RESULT.addNode(new Node("body")); RESULT.getBody().addListNode(cw); :}
             ;

CONTENIDO_WHILE::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_IF:bi INDENTACION2  CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(bi); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2  CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_FOR:bf INDENTACION2  CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(bf); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_WHILE:cw  {:RESULT = cw; :}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba  CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(ba);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_WHILE:cw {:RESULT = cw; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_WHILE:cw {:RESULT =cw; RESULT.add(bllf);:}
               ;
CONTENIDO_WHILE_FUNCTION::=newline INDENTACION2 BLOQUE_LECTURA:bl INDENTACION2  CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(bl); :}
               |newline INDENTACION2 BLOQUE_IF_FUNCTION:bi INDENTACION2  CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(bi); :}
               |newline INDENTACION2 BLOQUE_ESCRITURA:be INDENTACION2 CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(be); :}
               |newline INDENTACION2 BLOQUE_FOR_FUNCTION:bf INDENTACION2 CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(bf); :}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; :}
               |newline INDENTACION2 BLOQUE_ASIGNACION:ba CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(ba);:}
               |newline INDENTACION2 BLOQUE_RETURN:br CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(br);:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_WHILE_FUNCTION:cw {:RESULT = cw; RESULT.add(bc);:}
               |newline INDENTACION2 BLOQUE_LLAMADAS_FUNCIONES:bllf CONTENIDO_WHILE_FUNCTION:cw {:RESULT =cw; RESULT.add(bllf);:}
               ;


BLOQUE_ESTRUCTURA::=type espacio identificador:id INDENTACION2 CONTENIDO_ESTRUCTURA:ce end espacio type {:RESULT = new Stmt_Type("type"); RESULT.addNode(new Node(id)); RESULT.addNode(new Node("body")); RESULT.getBody().setNodes(ce); :}
                    ;

CONTENIDO_ESTRUCTURA::=newline INDENTACION2 DECLARACION_VARIABLE_ESTRUCTURA:dve INDENTACION2 CONTENIDO_ESTRUCTURA:ce {:RESULT = ce; RESULT.add(dve);:}
               |newline INDENTACION2 {:RESULT = new ArrayList();:}
               |newline INDENTACION2 CONTENIDO_ESTRUCTURA:ce {:RESULT = ce;:}
               |BLOQUE_COMENTARIOS:bc CONTENIDO_ESTRUCTURA:ce {:RESULT = ce; RESULT.add(bc);:}
               ;
DECLARACION_VARIABLE_ESTRUCTURA::=identificador:id espacio as espacio TIPOS:t {:RESULT = new Stmt_Variable("as"); RESULT.addNode(new Node(id)); RESULT.addNode(t);:}
                                 ;

BLOQUE_LECTURA::=identificador:id espacio asignacion espacio console punto read abrirparentesis cerrarparentesis {:RESULT = new Stmt_Lectura("read"); RESULT.addNode(new Node(id));:}
                 |ELEMENTOS_ESTRUCTUA:ee espacio asignacion espacio console punto read abrirparentesis cerrarparentesis {:RESULT = new Stmt_Lectura("read"); RESULT.addNode(new Node(ee));:}
                 |console punto read abrirparentesis cerrarparentesis {:RESULT = new Stmt_Lectura("read");:}
                 ;

BLOQUE_ESCRITURA::=console  punto write abrirparentesis EXP:e cerrarparentesis {:RESULT = new Stmt_Escritura("write"); RESULT.addNode(e);
                                                    if(e.getTipo_tabla().toString().equals("String") ||
                                                       e.getTipo_tabla().toString().equals("Integer") ||
                                                      e.getTipo_tabla().toString().equals("Boolean") ){
                                                    }else{
                                                         error_write_void(e.getVal(),e.getTipo_tabla().toString(), eleft, eright);
                                                    }
                                            :}
                 ;

BLOQUE_COMENTARIOS::=comentario:c {:RESULT = new Stmt_Comentario("comment"); RESULT.addNode(new Node(c));:}
                     ;






